{
  config,
  lib,
  pkgs,
  ...
}:
{
  environment.ldso32 = null;

  boot = {
    kernelPackages = pkgs.linuxPackages_latest; # pkgs.linuxPackages_cachyos, pkgs.linuxPackages_xanmod_latest, pkgs.linuxPackages_zen, pkgs.linuxPackages_lqx, linuxPackages_latest

    supportedFilesystems = [
      "ext4"
      "vfat"
      "tmpfs"
      "btrfs"
    ];

    swraid.enable = false;

    # Enable binfmt emulation of aarch64-linux, this is required for cross compilation.
    #binfmt.emulatedSystems = [ "aarch64-linux" ];

    loader = {
      timeout = 0;
      generationsDir.copyKernels = true;
      efi.canTouchEfiVariables = true;

      systemd-boot = {
        enable = true;
        configurationLimit = 15;
        consoleMode = "auto";
        editor = false;
        netbootxyz.enable = true;
      };
    };

    tmp = {
      # /tmp on tmpfs, lets it live on your ram
      # it defaults to FALSE, which means you will use disk space instead of ram
      # enable tmpfs tmp on anything except servers and builders
      useTmpfs = true;

      # If not using tmpfs, which is naturally purged on reboot, we must clean
      # /tmp ourselves. /tmp should be volatile storage!
      cleanOnBoot = true;

      # The size of the tmpfs, in percentage form
      # this defaults to 50% of your ram, which is a good default
      # but should be tweaked based on your systems capabilities
      tmpfsSize = "90%";
    };

    initrd = {
      # Verbosity of the initrd
      # disabling verbosity removes only the mandatory messages generated by the NixOS
      verbose = false;
      systemd = {
        # enable systemd in initrd
        enable = true;
        services.btrfs-rollback = {
          description = "Rollback btrfs root dataset to blank snapshot";
          wantedBy = [ "initrd.target" ];
          requires = [ "dev-disk-by\\x2dpartlabel-disk\\x2dmain\\x2droot.device" ];
          after = [ "dev-disk-by\\x2dpartlabel-disk\\x2dmain\\x2droot.device" ];
          before = [
            "-.mount"
            "sysroot.mount"
          ];
          unitConfig.DefaultDependencies = "no";
          serviceConfig.Type = "oneshot";
          script = ''
            mkdir /tmp -p
            MNTPOINT=$(mktemp -d)
            (
              mount -t btrfs -o subvol=/ /dev/disk/by-label/nixos "$MNTPOINT"
              trap 'umount "$MNTPOINT"' EXIT

              if [[ -e "$MNTPOINT/@root" ]]; then
                echo "Backing up current @root subvolume"
                mkdir -p "$MNTPOINT/old_roots"
                timestamp=$(date --date="@$(stat -c %Y "$MNTPOINT/@root")" "+%Y-%m-%d_%H:%M:%S")
                mv "$MNTPOINT/@root" "$MNTPOINT/old_roots/$timestamp"
              fi

              echo "Cleaning old backups (>7 days)"
              find "$MNTPOINT/old_roots" -maxdepth 1 -mtime +7 -type d | while read -r old_root; do
                echo "Deleting old backup: $old_root"
                btrfs subvolume delete "$old_root" || true
              done

              echo "Creating fresh @root subvolume"
              btrfs subvolume create "$MNTPOINT/@root"
            )
          '';
        };
      };
    };
  };
}
